/*
 * Copyright (c) 2018 Tobias Raatiniemi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

project(':plugin') {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        publications = ['mavenJava']
        publish = true

        pkg {
            repo = 'maven'
            name = 'sonar-objective-c-plugin'
            userOrg = user
            licenses = ['LGPL-3.0']
            labels = ['sonarqube', 'objective-c']

            websiteUrl = 'https://gitlab.com/raatiniemi/sonar-objective-c'
            issueTrackerUrl = 'https://gitlab.com/raatiniemi/sonar-objective-c/issues'
            vcsUrl = 'https://gitlab.com/raatiniemi/sonar-objective-c.git'

            publicDownloadNumbers = true

            version {
                name = projectVersion
                released = new Date()
                vcsTag = projectVersion
            }
        }
    }
}